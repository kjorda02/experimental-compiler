package experimental_compiler;
import java_cup.runtime.ComplexSymbolFactory.*;
import java_cup.runtime.*;
import java.util.HashMap;
import arbol.*;
import arbol.ref.*;
import arbol.val.*;
import arbol.type.*;
import arbol.flow.*;
import arbol.fun.*;
import datos.*;

parser code {:
    @Override
    public void unrecovered_syntax_error(Symbol cur_token) throws Exception {
        Main.report_error("Error sintàctic catastròfic", cur_token);
        done_parsing();        
    }

    @Override
    public void syntax_error(Symbol cur_token) {
        Main.report_error("Unexpected token", cur_token);
    }

    @Override
    public void report_fatal_error(String message, Object info) throws Exception {
        Main.report_error("Error catastròfic ("+message+")", info);
        done_parsing();
    }
:}

/* ===== Terminals (tokens from lexer) ===== */
terminal LKEY, RKEY;
terminal terminal_node RETURN, VOID, OUT, STRUCT, CONST, TYPE, IF, ELSE, WHILE, FOR, SWITCH, CASE, BREAK, PRINT, PUTCHAR, INPUT;
terminal terminal_node PLUS, NEG, MULT, DIV, MOD, LPAREN, RPAREN, SEMI, ASS, EXIT;
terminal AND, OR, NOT;
terminal LT, GT, LEQ, GEQ, EQ, NEQ;
terminal LBRAC, RBRAC;
terminal COMMA, DOT;
terminal terminal_node<basicType> INT, CHAR, BOOL, STRING;
terminal terminal_node<Integer> INTLIT;
terminal terminal_node<Integer> CHARLIT;
terminal terminal_node<Boolean> BOOLLIT;
terminal terminal_node<String> ID;

/* ========= Non-terminals ========= */
non terminal prog;
non terminal while_node while;
non terminal for_node forstmt;
non terminal for;
non terminal node forInit;
non terminal ifstmt_node ifstmt;
non terminal stmt_node blockstmt;
non terminal stmts_node stmts;
non terminal stmt_node stmt;
non terminal assign_node assign;

non terminal literal_expr_node literal;
non terminal expr_node atom_expr;
non terminal unaryOp_node unary_expr;
non terminal binaryOp_node binary_expr;
non terminal expr_node expr;

non terminal ref_node ref;

non terminal complexType type;
non terminal typedecl_node typedecl;
non terminal complexType.struct struct;
non terminal complexType.struct fields;

non terminal decl_node decl;

non terminal argDeclList_node argdecls;
non terminal arglist_node arglist;
non terminal complexType.funcsig funcsig;
non terminal fundecl_node fundecl;
non terminal fundecl_node partfundef;
non terminal fundecl_node fundef;
non terminal return_node return;
non terminal lkey;
non terminal rkey;

/* ========== Precedences ========== */
precedence left OR;
precedence left AND;
precedence left NOT;

precedence left EQ, NEQ;

precedence left LT, GT, LEQ, GEQ;

precedence left PLUS, NEG;
precedence left MULT;
precedence left DIV;
precedence left MOD;

start with prog;

/* ========== Grammar rules ========== */

fields  ::= type:t ID:i SEMI
            {: RESULT = new complexType.struct(null, i.value, t); :}
        | type:t ID:i SEMI fields:f
            {: RESULT = f.addField(i.value, t); :}

        | error
        | error fields
        ;

struct  ::= STRUCT lkey fields:f rkey
            {: RESULT = f; :}
            
            | struct lkey error rkey
        ;

type    ::= INT:t
            {: RESULT = new complexType.primitive(null, t.value); :}
        | CHAR:t
            {: RESULT = new complexType.primitive(null, t.value); :}
        | BOOL:t
            {: RESULT = new complexType.primitive(null, t.value); :}
        | STRING
        | LBRAC expr:e RBRAC type:t
            {: RESULT = new complexType.array(null, t, e); :}
        | MULT type:t
            {: RESULT = new complexType.pointer(null, t); :}
        | struct:s
            {: RESULT = s; :}

        | LBRAC error RBRAC type
        ;

typedecl    ::= TYPE ID type:t
        ;

decl    ::= type:t ID:i
            {: RESULT = new decl_node(t, i); :}
        | type:t ID:i ASS expr:e
            {: RESULT = new decl_node(t, i, e); :}
        | CONST:c type:t ID:i ASS expr:e
            {: RESULT = new decl_node(c.left, t, i, e); :}
        ;

arglist   ::= expr:e COMMA arglist:l
            {: RESULT = new arglist_node(e, l); :}
        |   expr:e
            {: RESULT = new arglist_node(e, null); :}
        |
            {: RESULT = null; :} 
        ;

ref ::= ID:i
            {: RESULT = new identifier_ref_node(i); :}
        | ref:r LBRAC expr:e RBRAC
            {: RESULT = new displ_node(r, e); :}
        | ref:r DOT ID:i
            {: RESULT = new field_node(r, i); :}
        | ID:i LPAREN arglist:l RPAREN:rp
            {: RESULT = new call_node(i, l, rp.right); :}

        | ref:r LBRAC error RBRAC
        | ref:r DOT error
        ;

literal ::= INTLIT:i
            {: RESULT = new literal_expr_node(i); :}
        | CHARLIT:c
            {: RESULT = new literal_expr_node(c); :}
        | BOOLLIT:b
            {: RESULT = new literal_expr_node(b); :}
        ;

atom_expr   ::= literal:e
            {: RESULT = new expr_node(e); :}
        | LPAREN expr:e RPAREN
            {: RESULT = e; :}
        | ref:r
            {: RESULT = new expr_node(r); :}
        | INPUT:i LPAREN RPAREN:r
            {: RESULT = new expr_node(new input_node(i.left, r.right)); :}

        | LPAREN error RPAREN
       ;

unary_expr  ::= atom_expr:e
            {: RESULT = new unaryOp_node(e); :}
        | NOT atom_expr:e
            {: RESULT = new unaryOp_node(OP.NOT, e); :}
        | NEG atom_expr:e
            {: RESULT = new unaryOp_node(OP.NEG, e); :}
        ;

binary_expr ::= unary_expr:e
            {: RESULT = new binaryOp_node(e); :}

        | binary_expr:e1 MOD binary_expr:e2
            {: RESULT = new binaryOp_node(OP.MOD, e1, e2); :}

        | binary_expr:e1 DIV binary_expr:e2
            {: RESULT = new binaryOp_node(OP.DIV, e1, e2); :}

        | binary_expr:e1 MULT binary_expr:e2
            {: RESULT = new binaryOp_node(OP.TIMES, e1, e2); :}
       
        | binary_expr:e1 PLUS binary_expr:e2
            {: RESULT = new binaryOp_node(OP.PLUS, e1, e2); :}

        | binary_expr:e1 NEG binary_expr:e2
            {: RESULT = new binaryOp_node(OP.NEG, e1, e2); :}

        | binary_expr:e1 LT binary_expr:e2
            {: RESULT = new binaryOp_node(OP.LT, e1, e2); :}

        | binary_expr:e1 GT binary_expr:e2
            {: RESULT = new binaryOp_node(OP.GT, e1, e2); :}

        | binary_expr:e1 LEQ binary_expr:e2
            {: RESULT = new binaryOp_node(OP.LEQ, e1, e2); :}

        | binary_expr:e1 GEQ binary_expr:e2
            {: RESULT = new binaryOp_node(OP.GEQ, e1, e2); :}

        | binary_expr:e1 EQ binary_expr:e2
            {: RESULT = new binaryOp_node(OP.EQ, e1, e2); :}

        | binary_expr:e1 NEQ binary_expr:e2
            {: RESULT = new binaryOp_node(OP.NEQ, e1, e2); :}

        | binary_expr:e1 AND binary_expr:e2
            {: RESULT = new binaryOp_node(OP.AND, e1, e2); :}

        | binary_expr:e1 OR binary_expr:e2
            {: RESULT = new binaryOp_node(OP.OR, e1, e2); :}
        ;

assign  ::= ref:r ASS expr:e
            {: RESULT = new assign_node(r, e); :}
        ;

expr    ::= binary_expr:e 
            {: RESULT = new expr_node(e); :}
        | assign:e
            {: RESULT = new expr_node(e); :}
        ;

lkey    ::= LKEY
            {: symbolTable.enterBlock(); :}
        ;

rkey    ::= RKEY
            {: symbolTable.exitBlock(); :}
        ;

ifstmt  ::= IF LPAREN expr:e RPAREN lkey stmts:s rkey
            {: RESULT = new ifstmt_node(e, s); :}
        ;

while   ::= WHILE LPAREN expr:e RPAREN lkey stmts:s rkey
            {: RESULT = new while_node(e, s); :}
        ;

return  ::= RETURN:r
            {: RESULT = new return_node(r); :}
        | RETURN:r expr:e
            {: RESULT = new return_node(r, e); :}
        ;

stmt    ::= decl:d
            {: RESULT = new stmt_node(d); :}
        | expr:e
            {: RESULT = new stmt_node(e); :}
        | return:r
            {: RESULT = new stmt_node(r); :}
        | PUTCHAR LPAREN expr:e RPAREN
            {: RESULT = new stmt_node(new print_node(e)); :}
        
        | error
            {: RESULT = new stmt_node(new node()); :}
        ;

forInit ::= decl:d
            {: RESULT = d; :}
        | expr:e
            {: RESULT = e; :}
        ;

for ::= FOR
            {: symbolTable.enterBlock(); :}
        ;

forstmt ::= for LPAREN forInit:init SEMI expr:cond SEMI expr:incr RPAREN LKEY stmts:s RKEY
            {: RESULT = new for_node(init, cond, incr, s); symbolTable.exitBlock(); :}
        | for LPAREN SEMI expr:cond SEMI expr:incr RPAREN LKEY stmts:s RKEY
            {: RESULT = new for_node(null, cond, incr, s); symbolTable.exitBlock(); :}
        | for LPAREN forInit:init SEMI SEMI expr:incr RPAREN LKEY stmts:s RKEY
            {: RESULT = new for_node(init, null, incr, s); symbolTable.exitBlock(); :}
        | for LPAREN forInit:init SEMI expr:cond SEMI RPAREN LKEY stmts:s RKEY
            {: RESULT = new for_node(init, cond, null, s); symbolTable.exitBlock(); :}
        | for LPAREN SEMI SEMI expr:incr RPAREN LKEY stmts:s RKEY
            {: RESULT = new for_node(null, null, incr, s); symbolTable.exitBlock(); :}
        | for LPAREN forInit:init SEMI SEMI RPAREN LKEY stmts:s RKEY
            {: RESULT = new for_node(init, null, null, s); symbolTable.exitBlock(); :}
        | for LPAREN SEMI expr:cond SEMI RPAREN LKEY stmts:s RKEY
            {: RESULT = new for_node(null, cond, null, s); symbolTable.exitBlock(); :}
        | for LPAREN SEMI SEMI RPAREN LKEY stmts:s RKEY
            {: RESULT = new for_node(null, null, null, s); symbolTable.exitBlock(); :}
        ;

blockstmt ::= ifstmt:i
            {: RESULT = new stmt_node(i); :}
        | while:w
            {: RESULT = new stmt_node(w); :}
        | forstmt:f
            {: RESULT = new stmt_node(f); :}
        ;

stmts    ::= stmt:s SEMI
            {: RESULT = new stmts_node(s); :}
        | stmt:s SEMI stmts:ss
            {: RESULT = new stmts_node(s, ss); :}
        | blockstmt:b
            {: RESULT = new stmts_node(b); :}
        | blockstmt:b stmts:ss
            {: RESULT = new stmts_node(b, ss); :}
        ;

argdecls ::= type:t ID:i COMMA argdecls:l
            {: RESULT = new argDeclList_node(t, i, l, false); :}
        |   type:t ID:i
            {: RESULT = new argDeclList_node(t, i, null, false); :}
        |   OUT type:t ID:i COMMA argdecls:l
            {: RESULT = new argDeclList_node(t, i, l, true); :}
        |   OUT type:t ID:i
            {: RESULT = new argDeclList_node(t, i, null, true); :}
        |
            {: RESULT = null; :} 
        ;

funcsig  ::= type:t ID:i LPAREN argdecls:l RPAREN
            {: RESULT = new complexType.funcsig(t, i, l); :}
        | VOID ID:i LPAREN argdecls:l RPAREN
            {: RESULT = new complexType.funcsig(null, i, l); :}
        ;

fundecl ::= funcsig:sig SEMI
            {: RESULT = new fundecl_node(sig, false); :}
        | funcsig:sig
            {: RESULT = new fundecl_node(sig, true); :}
        ;

partfundef ::= fundecl:f lkey
            {: f.addArgs(); RESULT = f; :}
        ;

fundef ::= partfundef:f stmts:s rkey
            {: f.setStmts(s); RESULT = f; :}
        | partfundef:f rkey
            {: f.setStmts(null); RESULT = f;:}
        ;

prog    ::= fundecl:d
            {: d.gest(); :}
        | prog:p fundecl:d
            {: d.gest(); RESULT = p; :}
        | fundef:d
            {: d.gest(); :}
        | prog:p fundef:d
            {: d.gest(); RESULT = p; :}
        | typedecl:d SEMI
            {: d.gest(); :}
        | prog:p typedecl:d
            {: d.gest(); RESULT = p; :}
        | decl:d SEMI
            {: d.gest(); :}
        | prog:p decl:d SEMI
            {: d.gest(); :}
        ;
